plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.3'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'    // asciidoctor 플러그인 추가
}

group = 'sample'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExt // asciidoctor를 확장하는 종속성에 대한 구성 선언
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring boot
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // h2
    runtimeOnly 'com.h2database:h2'

    // RestDocs
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('asciidoctor') {
    dependsOn test  // test 먼저 수행
    // 테스트 코드 실행 시 document()를 읽어 build/generated-snippets에 .adoc 문서 생성

    configurations 'asciidoctorExt'
    // src/docs/asciidoc 하위 경로에 작성한 .adoc 문서들을 build/docs/asciidoc에 html 문서로 생성

    sources {
        include("**/index.adoc")  // build/docs/asciidoc 하위 경로에 index.html만 생성
    }
    baseDirFollowsSourceFile() // 다른 adoc 파일을 include 할 때 경로를 baseDir로 맞춤
}

tasks.register('copyRestDocs', Copy) {
    dependsOn asciidoctor // asciidoctor 먼저 수행
    doFirst {
        delete file("src/main/resources/static/docs") // 기존 RestDoc html 문서 삭제
    }
    from asciidoctor.outputDir
    into file("src/main/resources/static/docs") // asciidoctor task에 의해 생긴 html 문서 복사
}

build {
    dependsOn copyRestDocs  // copyRestDocs 먼저 수행
}


